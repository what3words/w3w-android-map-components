plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.dokka' version '1.5.0'
}

def libVersion = findProperty('LIBRARY_VERSION') ? findProperty('LIBRARY_VERSION') : "1.0-debug"

apply from: 'maven-push.gradle'
apply from: '../jacoco.gradle'

android {
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        buildConfigField("String", "VERSION_NAME", "\"${libVersion}\"")
    }

    def filesToExclude = [
            '**/*SdkToApiExtensions*.kt',
            '**/*W3WSdkDataSource*.kt'
    ]

    tasks.withType(SourceTask.class).configureEach {
        it.exclude(filesToExclude)
    }
    android.sourceSets.main.kotlin.exclude(filesToExclude)

    buildTypes {
        debug {
            testCoverageEnabled = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'com.what3words.map.components'
    tasks.dokkaGfm.configure {
        suppressObviousFunctions.set(true)
        suppressInheritedMembers.set(true)
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    // android
    implementation "com.google.android.material:material:$material_version"

    // what3words
    api "com.what3words:w3w-android-wrapper:$what3words_android_wrapper_version"
    api "com.what3words:w3w-android-design-library:$what3words_android_design_version"

    // Google maps
    compileOnly "com.google.android.gms:play-services-maps:$gms_version"
    compileOnly "com.google.maps.android:android-maps-utils:$gms_utils_version"
    testImplementation "com.google.android.gms:play-services-maps:$gms_version"

    // Mapbox
    compileOnly "com.mapbox.maps:android:$mapbox_version"

    // kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Testing
    testImplementation "junit:junit:$junit_version"
    testImplementation "androidx.test:core:$androidx_test_core"
    testImplementation "com.google.truth:truth:$google_truth"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "androidx.arch.core:core-testing:$androidx_arch_core"
}